import java.io.ByteArrayOutputStream

plugins {
    id("java-library")
    id("maven-publish")
    id("com.gradleup.shadow") version "8.3.11"
}

subprojects {
    plugins.apply("java-library")
    plugins.apply("maven-publish")
    plugins.apply("com.gradleup.shadow")

    group = "${project.property("group")}"
    version = "${project.property("version")}.${commitsSinceLastTag()}"

    java {
        toolchain {
            languageVersion.set(JavaLanguageVersion.of(25))
        }
        withSourcesJar()
        disableAutoTargetJvm()
    }

    tasks {
        withType<JavaCompile> {
            options.encoding = "UTF-8"
            // The common engine and the Sponge (SpongeAPI 7.4 / MC 1.12.2) module must run on
            // Java 8; all other platform modules target 21.
            options.release = if (project.name == "bolt-common" || project.name == "bolt-sponge") 8 else 21
        }
        jar {
            archiveClassifier.set("noshade")
        }
        shadowJar {
            archiveClassifier.set("")
            archiveFileName.set("${project.property("artifactName")}-${project.version}.jar")
        }
        build {
            dependsOn(shadowJar)
        }
    }

    publishing {
        repositories {
            if (project.hasProperty("mavenUsername") && project.hasProperty("mavenPassword")) {
                maven {
                    credentials {
                        username = "${project.property("mavenUsername")}"
                        password = "${project.property("mavenPassword")}"
                    }
                    url = uri("https://repo.codemc.io/repository/maven-releases/")
                }
            }
        }
        publications {
            create<MavenPublication>("maven") {
                groupId = "${project.group}"
                artifactId = project.name
                version = "${project.version}"
                from(components["java"])
            }
        }
    }
}

fun commitsSinceLastTag(): String {
    val result = project.providers.exec {
        commandLine("git", "describe", "--tags", "--always")
    }
    val tagDescription = result.standardOutput.asText.get()
    if (tagDescription.indexOf('-') < 0) {
        return "0"
    }
    return tagDescription.split('-')[1]
}
